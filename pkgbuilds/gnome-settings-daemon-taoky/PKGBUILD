# Maintainer: Pellegrino Prevete (tallero) <pellegrinoprevete@gmail.com>

_pkgname=gnome-settings-daemon
pkgname="${_pkgname}-taoky"
pkgver=49.0
pkgrel=1
pkgdesc="GNOME Settings Daemon"
_gnome_git="https://gitlab.gnome.org/GNOME"
url="https://gitlab.gnome.org/taoky/${_pkgname}"
arch=(x86_64)
license=(
  GPL-2.0-or-later
  LGPL-2.0-or-later
)
depends=(
  alsa-lib
  bash
  dconf
  fontconfig
  gcc-libs
  gcr-4
  geoclue
  geocode-glib-2
  glib2
  glibc
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  libcanberra-pulse
  libcolord
  libcups
  libgudev
  libgweather-4
  libmm-glib
  libnm
  libnotify
  libp11-kit
  libpulse
  libx11
  libxfixes
  polkit
  pulse-native-provider
  systemd-libs
  upower
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  libxslt
  meson
  python
  usbguard
)
checkdepends=(
  python-dbusmock
  python-gobject
)
optdepends=('usbguard: USB protection support')
groups=(gnome)
backup=(etc/xdg/Xwayland-session.d/00-xrdb)
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://gitlab.gnome.org/GNOME/gnome-settings-daemon.git#tag=${pkgver/[a-z]/.&}"
        "git+${_gnome_git}/libgnome-volume-control.git"
        "https://gitlab.gnome.org/taoky/gnome-settings-daemon/-/commit/6e0d15d1827dabbc56a69c4468138fc330a25696.diff")
b2sums=('87acacd2b77f527811384665a2a29d3ab67656dd9e800c37168dab4db478825afe967442ebbbe146f4f9c54f9c256d1c9e2f8010a49b8bc0ec1b5f655a96d41d'
            'SKIP'
            'SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --tags | \
   sed 's/^GNOME_SETTINGS_DAEMON_//;s/_/./g;s/-/+/g'
  git apply ../6e0d15d1827dabbc56a69c4468138fc330a25696.diff
}

prepare() {
  cd "${_pkgname}"

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson "${_pkgname}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:

