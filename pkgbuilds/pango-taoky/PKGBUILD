# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pango-taoky
pkgname=(
  pango-taoky
  pango-taoky-docs
)
pkgver=1.57.0
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  cairo
  fontconfig
  freetype2
  fribidi
  glib2
  glibc
  harfbuzz
  libsysprof-capture
  libthai
  libx11
  libxft
  libxrender
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  help2man
  meson
  python-docutils
)
source=("git+https://gitlab.gnome.org/GNOME/pango.git#tag=$pkgver"
	"romn.patch")
b2sums=('957e4b9999013065fdc3f175ee448fbc5a7580f0ffecfd4649234485b19e9911dd78ca28b728552b5a2577820ea1c6c9aefad5e016d236c379613ce0aaf77953'
	'40122bcff57b7b4a3ce9e2ee91d21e2b72ed259889c75039da60d45f080f179019eea4cdd4e86d9e7ee45449fc46e0a776bdf1abed3764113cbf64f8da2c377d')

prepare() {
  cd pango
  git apply ../romn.patch
}

build() {
  local meson_options=(
    -D documentation=true
    -D man-pages=true
    -D sysprof=enabled
  )

  arch-meson pango build "${meson_options[@]}"
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_pango-taoky() {
  provides=(pango libpango{,cairo,ft2,xft}-1.0.so)
  conflicts=(pango)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-taoky-docs() {
  pkgdesc+=" (documentation)"
  provides=(pango-docs)
  conflicts=(pango-docs)
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
