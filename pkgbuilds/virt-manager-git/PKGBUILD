# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

_pkgbase=virt-manager
pkgbase=$_pkgbase-git
pkgname=(virt-install-git virt-manager-git)
pkgver=4.1.0.r322.gc6bc3a866
pkgrel=1
arch=('any')
url='https://virt-manager.org/'
license=('GPL')
makedepends=('python-docutils' 'python-setuptools')
checkdepends=('python-pytest' 'libosinfo' 'libvirt-python' 'python-gobject' 'python-requests' 'cpio' 'cdrtools')
optdepends=('x11-ssh-askpass: provide password for remote machines connected via ssh tunnel')
source=("git+https://github.com/virt-manager/virt-manager.git")
b2sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgbase"
  python setup.py configure --default-hvs qemu,lxc
  python setup.py build
}

package_virt-install-git() {
  pkgdesc='Command line tool for creating new KVM , Xen, or Linux container guests using the libvirt hypervisor'
  depends=('libosinfo' 'libvirt-python' 'python-gobject' 'python-requests' 'cpio' 'libisoburn')
  provides=('virt-install')
  conflicts=('virt-install')

  cd "$srcdir/$_pkgbase"
  python setup.py --no-update-icon-cache --no-compile-schemas install --root "${pkgdir}"
  python -m compileall "${pkgdir}"/usr/share/virt-manager
  python -O -m compileall "${pkgdir}"/usr/share/virt-manager

  # Split virt-manager
  [[ -d "${srcdir}"/virt-manager ]] && rm -r "${srcdir}"/virt-manager/
  mkdir -p "${srcdir}"/split/usr/{bin,share/{man/man1,virt-manager}}
  mv "${pkgdir}"/usr/bin/virt-manager "${srcdir}"/split/usr/bin/
  mv "${pkgdir}"/usr/share/{applications,glib-2.0,icons,metainfo} "${srcdir}"/split/usr/share/
  mv "${pkgdir}"/usr/share/man/man1/virt-manager.1 "${srcdir}"/split/usr/share/man/man1/
  mv "${pkgdir}"/usr/share/virt-manager/{icons,ui,virtManager} "${srcdir}"/split/usr/share/virt-manager/
}

package_virt-manager-git() {
  pkgdesc='Desktop user interface for managing virtual machines'
  depends=("virt-install-git=${pkgver}" 'gtk-vnc' 'libvirt-glib' 'spice-gtk' 'vte3' 'python-cairo' 'gtksourceview4')
  provides=('virt-manager')
  conflicts=('virt-manager')

  mv -v split/* "${pkgdir}/"
}

